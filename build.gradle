buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath 'de.undercouch:gradle-download-task:4.1.2'
    }
}

plugins {
  id "org.sonarqube" version "3.0"
}

configurations.all {
    resolutionStrategy {
        force 'cpw.mods:securejarhandler:0.9.50'
    }
}

apply from: 'https://raw.githubusercontent.com/ldtteam/OperaPublicaCreator/main/gradle/mod.gradle'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

configurations {
    library
    implementation.extendsFrom library
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

dependencies {
    implementation fg.deobf("mezz.jei:jei-${project.minecraftVersion}:${project.jei_version}")

    apiImplementation fg.deobf("com.ldtteam:blockui:${project.minecraftVersion}-${project.blockUI_version}"){
        transitive = false
    }
    implementation fg.deobf("com.ldtteam:blockui:${project.minecraftVersion}-${project.blockUI_version}"){
        transitive = false
    }

    apiImplementation fg.deobf("com.ldtteam:structurize:${project.minecraftVersion}-${project.structurize_version}"){
        transitive = false
    }
    implementation fg.deobf("com.ldtteam:structurize:${project.minecraftVersion}-${project.structurize_version}"){
        transitive = false
    }

    implementation fg.deobf("com.ldtteam:multi-piston:${project.exactMinecraftVersion}-${project.multiPistonVersion}"){
        transitive = false
    }

    datagenLibrary ("com.ldtteam:datagenerators:${project.dataGeneratorsVersion}"){
        transitive = false
    }
    library ("com.ldtteam:datagenerators:${project.dataGeneratorsVersion}") {
        transitive = false
    }
}
